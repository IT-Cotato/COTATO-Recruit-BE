plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.diffplug.spotless' version '6.25.0'
}

group = 'org.cotato.backend'
version = '2026.02.17.1'
description = 'Cotato Recruit project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencyManagement {
	imports {
		mavenBom 'software.amazon.awssdk:bom:2.41.1'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// Spring Security & OAuth2
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

	// Redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// Cache (Caffeine)
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'com.github.ben-manes.caffeine:caffeine'

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

	// Swagger (SpringDoc OpenAPI)
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

	// Email
	implementation 'org.springframework.boot:spring-boot-starter-mail'

	// AWS S3
	implementation 'software.amazon.awssdk:s3'
	implementation 'software.amazon.awssdk:aws-crt-client'

	// Sentry Spring Boot Starter
	implementation platform("io.sentry:sentry-bom:8.30.0")
	implementation "io.sentry:sentry-spring-boot-starter"
	implementation "io.sentry:sentry-logback"

	// Database
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.mysql:mysql-connector-j'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.apache.poi:poi-ooxml:5.2.3'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testRuntimeOnly 'com.h2database:h2'
}

tasks.named('test') {
	useJUnitPlatform()
}

spotless {
	java {
		target 'src/**/*.java'

		// Google Java Format 사용
		googleJavaFormat('1.19.2').aosp().reflowLongStrings()

		// import 정리
		importOrder()
		removeUnusedImports()

		// 파일 끝 개행 처리
		endWithNewline()

		// 들여쓰기를 탭으로 통일
		indentWithTabs()
		trimTrailingWhitespace()
	}

	format 'misc', {
		target '**/*.md', '**/*.yml', '**/*.yaml', '**/.gitignore'
		trimTrailingWhitespace()
		endWithNewline()
	}
}

// Pre-commit 훅 설정
tasks.register('installGitHooks', Copy) {
	description = 'Git pre-commit 훅을 설치합니다.'
	from file("${rootDir}/scripts/pre-commit")
	into file("${rootDir}/.git/hooks")
	filePermissions {
		unix(0755)
	}
}

// 빌드 시 자동으로 훅 설치 및 포맷팅 적용
tasks.named('build') {
	dependsOn installGitHooks
	dependsOn spotlessApply
}

// 컴파일 전에 자동으로 포맷팅 적용
tasks.named('compileJava') {
	dependsOn spotlessApply
}
